plugins {
	id 'babric-loom' version '1.4.+'
	id 'java'
}

group = "sunsetsatellite"
archivesBaseName = "catalyst-all"
version = project.mod_version

loom {
	gluedMinecraftJar()
	noIntermediateMappings()
	customMinecraftManifest.set("https://github.com/Turnip-Labs/bta-manifest-repo/releases/download/v${project.bta_version}/${project.bta_version}.json")
}

dependencies {
	minecraft "bta-download-repo:bta:${project.bta_version}"
	mappings loom.layered() {}

	if(findProject(":catalyst") == null){
		include(project(":"))
		include(rootProject.project("energy"))
		include(rootProject.project("fluids"))
		include(rootProject.project("multiblocks"))
		include(rootProject.project("effects"))
	} else {
		include(project(":catalyst"))
		include(project(":catalyst").project("energy"))
		include(project(":catalyst").project("fluids"))
		include(project(":catalyst").project("multiblocks"))
		include(project(":catalyst").project("effects"))
	}

}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.release.set 8
}

remapJar {
	destinationDirectory.set(file("$parent.projectDir/jars"))
	archiveVersion.set(archiveVersion.get()+"-${bta_version}")
}

